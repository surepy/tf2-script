///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////vacc script///////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//==================================================================================//	
// This medigun vaccinator script is supposed to make the vaccinator easy to use.	//
// bind vaxKey1-4 and r to use the script, then run vaxStartUp.						//
// Pressing 1 should switch to bullet, 2 to blast, and 3 to fire.					//
// If the script ever gets the resistance wrong, you can always use r to 			//
// select the bullet resistance and press 4 to reset the script. 					//																
// The script has reset when it reloads 3 times, do this after death.				//																																								
//==================================================================================//

echo "- aliashook: medic quicVac"
echo "-   Pressing 1 should switch to bullet, 2 to blast, and 3 to fire."
echo "-   If the script ever gets the resistance wrong, you can always use r to select the bullet resistance and press 4 to reset the script."

// Run to startup the script																																		
alias vaxStartUp "vaxWaitTest"																																	
// A wait tester as the vaccinator script wont work without wait																									
alias vaxWaitTest "alias vaxWaitTmp vaxEnable; alias wait vaxWaitSetF; wait; vaxWaitTmp"																			
alias vaxWaitSetF "alias vaxWaitTmp vaxUnbind; exec aliashook/misc/notifyVaccFailed"																	
																																									
// Use to enable/disable the script, startup is reccomended to check for wait																						
alias vaxEnable "vaxBindToBullet; uiToVax; exec aliashook/misc/notifyVaccEnabled"																		
alias vaxDisable "vaxUnbind; uiResetVax; notifyQuickVacOff; echo Disabling Vaccinator script"																							
																																									
alias vaxUnbind "alias vaxKey1; alias vaxKey2; alias vaxKey3; alias vaxKey4; alias vaxKeyR"																			
																																									
// Binds 1-3 and r respectivly to switch to a resistance																											
alias vaxBindToBullet "alias vaxKey1; alias vaxKey2 vaxBulletToBlast; alias vaxKey3 vaxBulletToFire; alias vaxKey4; alias vaxKeyR vaxBulletToBlast"					
alias vaxBindToBlast "alias vaxKey1 vaxBlastToBullet; alias vaxKey2; alias vaxKey3 vaxBlastToFire; alias vaxKey4 vaxCorrectResistance; alias vaxKeyR vaxBlastToFire"
alias vaxBindToFire "alias vaxKey1 vaxFireToBullet; alias vaxKey2 vaxFireToBlast; alias vaxKey3; alias vaxKey4 vaxCorrectResistance; alias vaxKeyR vaxFireToBullet"	

//Vax calls for a certin ammount of reload presses 							
alias vaxRl1 "slot2; wait 2; +reload; wait 15; -reload"						
alias vaxRl2 "slot2; wait 2; +reload; wait 15; -reload; wait 8 vaxRl1"		
alias vaxRl3 "slot2; wait 2; +reload; wait 15; -reload; wait 8 vaxRl2"		
																			
// Aliases for switching resistances										
// First unbind players key to prevent user from pressing multiple at one,	
// reload an amount of times, wait for the reload to finish,				
// then we rebind our keys													
alias vaxBulletToBlast "vaxUnbind; vaxRl1; wait 120; vaxBindToBlast"		
alias vaxBlastToFire "vaxUnbind; vaxRl1; wait 120; vaxBindToFire"			
alias vaxFireToBullet "vaxUnbind; vaxRl1; wait 120; vaxBindToBullet"		
alias vaxBulletToFire "vaxUnbind; vaxRl2; wait 120; vaxBindToFire"			
alias vaxFireToBlast "vaxUnbind; vaxRl2; wait 120; vaxBindToBlast"			
alias vaxBlastToBullet "vaxUnbind; vaxRl2; wait 120; vaxBindToBullet"		
																			
// Wrong Resistance correction												
// When we die, our resistance resets to bullet, pressing the reset key will
// reset the script so that it knows about the change. 						
// It also reloads three times as a notifacation that the reset happened	
alias vaxCorrectResistance "vaxUnbind; vaxRl3; wait 130; vaxBindToBullet"


alias vaxNext vaxRl1
alias vaxPrevious vaxRl2

// input: mouse2 - previous resistance / mouse3 - next resistance
alias uiUseVacButtons "alias uiButtonKey2 vaxPrevious; alias uiButtonKey3 vaxNext"

echo "- aliashook: medic quicVac done"