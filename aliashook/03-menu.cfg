echo "- aliashook: console menu"

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////menu///////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//==================================================================================//
// This script allows a menu to be used by the user using the development console.	//
// You can browse the menu using the aliases provided below.						//
// You have many options to go into sub menus and exit from the gui itself.			//
//																					//
// This can be seperated frum the hudConsoleApi but be warned, without use of 		//
// the hudConsoleApi could allow any other alias to disable the dev console			//
// making the script not visible anymore.											//
//==================================================================================//

//==============================================//
// Bind "gToggleGui" to toggle menu visibility	//
// Bind "gPointerUp" to move pointer up			//
// Bind "gPointerDown" to move pointer down		//
// Bind "gPointerSelect" to select menu options	//
//==============================================//

//////////////////////////////////////////////////////////////////////////////////////////	
// Automated string buffer pusher

// We dont need this, because con_filter_text is a much better way of solving this issue, but this code will stay.
// Main Display Loop, we start it by using the wait detector
alias gLoopStart "alias gLoopRedirect gLoopWaitT; gLoopWaitT"
alias gLoopStop "alias gLoopRedirect; hudOffali1"
alias gLoop "gPushUpdate; wait 30; gLoopRedirect"

// Test for wait
alias gLoopwaitT "alias gLoopWaitTmp gLoop; alias wait gLoopSetwaitF; wait; gLoopWaitTmp"
// When wait fails, we just let dev console show and we push an update but dont loop
alias gLoopSetwaitF "alias gLoopWaitTmp; gPushUpdate"

// Push an update from string buffer to console
// a_dumb = set con_filter_text to <ui>, a_dumb_2 = unset con_filter_text
alias gPushUpdate "exec a_dumb; hudOnali1; bline1; bline2; bline3; bline4; bline5; bline6; bline7; bline8; bline9; exec a_dumb_2; echo "

// bline9 = contimes 11
contimes 12

// reset all blines. 
alias gResetLines "hudOffali1; alias bline1 echo <ui>; alias bline2 echo <ui>; alias bline3 echo <ui>; alias bline4 echo <ui>; alias bline5 echo <ui>; alias bline6 echo <ui>; alias bline7 echo <ui>; alias bline8 echo <ui>; alias bline9 echo <ui>"
gResetLines

//////////////////////////////////////////////////////////////////////////////////////////	

//////////////////////////////////////////////////////////////////////////////////////////	
// Aliases to control user input																					
																													
// We use redirects to control whether user input is allowed														
alias gEnableUserInput "alias gPointerUp gPointUp; alias gPointerDown gPointDown; alias gPointerSelect gPointSel"	
alias gDisableUserInput "alias gPointerUp; alias gPointerDown; alias gPointerSelect"								
																													
// use to toggle the gui																							
alias gToggleGuiOn "alias gToggleGui gToggleGuiOff; gEnableUserInput; gMainMenu" // Set main menu here	
alias gToggleGuiOff "alias gToggleGui gToggleGuiOn; gDisableUserInput; hudOffali1; gLoopStop"									
// Call the this to alias to prevent "command not found" in console													
gToggleGuiOff

exec aliashook/menu/01-mainMenu.cfg
exec aliashook/menu/02-subMenu01.cfg
exec aliashook/menu/03-subMenu02.cfg
exec aliashook/menu/04-subMenu03.cfg
exec aliashook/menu/05-subMenu04.cfg
exec aliashook/menu/06-subMenu05.cfg

echo "- aliashook: console menu done"